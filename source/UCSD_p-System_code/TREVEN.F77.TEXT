$USES UPROUT IN TREK2A:TRPROU.F77
$USES USKIP IN TREK2A:TRSKIP.F77
$USES UIRAN8 IN TREK2A:TRIRA8.F77
$USES RANDOM IN TREK2A:TRRANF.PAS
$USES UEXPRAN IN TREK2A:TREXPR.F77
$USES UCRAM IN TREK2A:TRCRAM.F77
$USES UCREND IN TREK2A:TRCREN.F77
$USES UCRAMDM IN TREK2A:TRCRDM.F77
$USES UCRAMLO IN TREK2A:TRCRML.F77
$USES UNEWQUA IN TREK2A:TRNEWQ.F77
$USES UNEWCON IN TREK2A:TRNEWC.F77
$USES UCRAMSH IN TREK2B:TRCRSH.F77
$USES USOS IN TREK2B:TRSOS.F77
$USES UFINISH IN TREK2B:TRFINI.F77
$USES UAUTOVE IN TREK2B:TRAUTO.F77
$USES USNOVA IN TREK2B:TRSNOV.F77
$USES USCOM IN TREK2B:TRSCOM.F77
$USES USHIELD IN TREK2B:TRSHIE.F77
      SUBROUTINE EVENTS
$INCLUDE COMP1B2:TREKCO.F77
      EQUIVALENCE (CRACKS(2),SHUTUP)
      CHARACTER*240 OLINE
      COMMON/MYBUF/OLINE
      CHARACTER*240 ITEM
      COMMON/SCNBF1/ITEM
      REAL PICTUR(115)
      EQUIVALENCE (PICTUR,DATE),(CRACKS(5),ITYPE)
      CHARACTER*1 CH1
      INTRINSIC AMAX1,SQRT,ICHAR,MOD
      DATA NEVENT/7/
C
      ICTBEA=0
      ISTRAC=0
C--------SELECT EARLIEST EXTRANEOUS EVENT (LINE=0 IF NO EVENTS)
 10   LINE=0
      IF(ALLDON.NE.0) RETURN
      DATEMI=DATE+TIME
      DO 20 L=1,NEVENT
      IF(FUTURE(L) .GT. DATEMI) GO TO 20
      LINE=L
      DATEMI=FUTURE(L)
 20   CONTINUE
      XTIME=DATEMI-DATE
      DATE=DATEMI
C--------DECREMENT FEDERATION RESOURCES AND RECOMPUTE REMAINING TIME
      REMRES=REMRES-(REMKL+4*REMCOM)*XTIME
      REMTIM=REMRES/(REMKL+4*REMCOM)
      IF(REMTIM .GT. 0) GO TO 30
C--------FEDERATION RESOURCES DEPLETED; END
      CALL FINISH(2)
      RETURN
C--------DECIDE IF LIFE SUPPORT IS ADEQUATE
 30   IF(DAMAGE(5).EQ.0 .OR. CONDIT.EQ.IHDOCK ) GO TO 50
      IF(LSUPRE .GE. XTIME .OR. DAMAGE(5) .LE. LSUPRE) GO TO 40
      CALL FINISH(3)
      RETURN
 40   LSUPRE=LSUPRE-XTIME
      IF(DAMAGE(5) .LE. XTIME) LSUPRE=INLSR
C--------FIX DEVICES
 50   REPAIR=XTIME
      IF(CONDIT .EQ. IHDOCK ) REPAIR=XTIME/DOCKFA
      DO 60 L=1,NDEVIC
      IF(L.EQ.14) GO TO 60
      IF(DAMAGE(L) .GT. 0) DAMAGE(L)=AMAX1(DAMAGE(L)-REPAIR,0.0)
60    CONTINUE
C--------CAUSE EXTRANEOUS EVENT [<LINE>] TO OCCUR
      TIME=TIME-XTIME
      IF(LINE .EQ. 0) GO TO 5000
      GO TO (100,200,300,400,500,600,700),LINE
C--------EXTRANEOUS EVENT 1:  SUPERNOVA
 100  CALL SNOVA(0,0)
      FUTURE(1)=DATE+EXPRAN(0.5*INTIME)
      IF(GALAXY(QUADX,QUADY) .EQ. 1000) RETURN
      GO TO 10
C--------EXTRANEOUS EVENT 2:  TRACTOR BEAM
 200  IF(REMCOM .EQ. 0) GO TO 220
      IF(ISTRAC.NE.0) GO TO 210
      IF(CONDIT .EQ. IHDOCK ) GO TO 210
      RF=RANF()
      I=RF*REMCOM+1.0
      YANK=(CX(I)-QUADX)**2 + (CY(I)-QUADY)**2
      IF(YANK .EQ. 0 .AND. JUSTIN .EQ. 0) GO TO 210
      IF(ISTRAC .EQ. 0) GO TO 201
20010 YANK=(ISX-QUADX)**2+(ISY-QUADY)**2
 201  YANK=SQRT(YANK)
      YNKRAT=7.5
      TIME=(10.0/YNKRAT**2)*YANK
      ICTBEA=1
      CALL SKIP(1)
      CALL CRM3AS
      CALL CRAMSH
      ITEM=' CAUGHT IN LONG-RANGE TRACTOR BEAM--'
      CALL CRAMDM(36)
C--------IF KIRK AND CO. SCREWING AROUND ON PLANET, HANDLE.
C     CALL GRAB
      CALL AUTOVE(1)
      IF(ALLDON.NE.0) RETURN
C--------HANDLE CASE WHERE KIRK IS IN SHUTTLE CRAFT.
      IF(ICRAFT .NE. 1) GO TO 203
      CALL FINISH(18)
      RETURN
C--------CHECK TO SEE IF SHUTTLE IS ABOARD.
 203  IF(ISCRAF .NE. 0) GO TO 204
      CALL SKIP(1)
      OLINE='GALILEO, LEFT ON THE PLANET SURFACE, IS CAPTURED'
      CALL PROUT(48)
      OLINE='BY ALIENS AND MADE INTO A FLYING MC DONALD''S.'
      CALL PROUT(45)
      DAMAGE(10)=-10.
      ISCRAF=-1
 204  IF(ISTRAC .EQ. 0) GO TO 205
      QUADX=ISX
      QUADY=ISY
      GO TO 206
 205  QUADX=CX(I)
      QUADY=CY(I)
 206  CALL IRAN10(SECTX,SECTY)
      ITEM='   PULLED TO'
      CALL CRAM(12)
      CALL CRAMLO(1,QUADX,QUADY)
      ITEM=', '
      CALL CRAM(2)
      CALL CRAMLO(2,SECTX,SECTY)
      CALL CREND
      IF(RESTIN .NE. 0) THEN
      OLINE='(REMAINDER OF REST & REPAIR PERIOD CANCELLED.)'
      CALL PROUT(46)
      ENDIF
      RESTIN=0
C     CALL CANTA
      IF(SHLDUP.NE.0) GO TO 208
      IF(DAMAGE(8).EQ.0 .AND. SHLD.GT.0) GO TO 207
      OLINE='(SHIELDS NOT CURRENTLY USEABLE.)'
      CALL PROUT(32)
      GO TO 208
C207  CALL SHLDSUP
 207  CALL SHIELD(1)
      SHLDCH=0
 208  CALL NEWQUA
      IF(REMCOM .LE. 0) GO TO 220
 210  FUTURE(2)=DATE+TIME+EXPRAN(1.5*INTIME/REMCOM)
      GO TO 10
 220  FUTURE(2)=1E38
      GO TO 10
C--------EXTRANEOUS EVENT 3:  SNAPSHOT OF UNIVERSE (FOR TIME WARP)
 300  DO 310 L=1,115
 310  SNAPSH(L)=PICTUR(L)
      SNAP=1
      FUTURE(3)=DATE+EXPRAN(0.5*INTIME)
      GO TO 10
C--------EXTRANEOUS EVENT 4:  COMMANDER ATTACKS STARBASE
C--------LOOK FOR A COMMANDER IN SAME QUADRANT AS A STARBASE
 400  IF(REMCOM.GT.0 .AND. REMBAS.GT.0) GO TO 410
      FUTURE(4)=1E38
      FUTURE(5)=1E38
      GO TO 10
 410  DO 420 J=1,REMBAS
      DO 420 K=1,REMCOM
      IF( (BASEQX(J).EQ.CX(K) .AND. BASEQY(J).EQ.CY(K)) .AND.
     +    (BASEQX(J).NE.QUADX .OR.  BASEQY(J).NE.QUADY) .AND.
     +    (BASEQX(J).NE.ISX .OR. BASEQY(J).NE.ISY) ) GO TO 430
 420  CONTINUE
      FUTURE(4)=DATE+EXPRAN(0.3*INTIME)
      FUTURE(5)=1E38
      GO TO 10
C--------COMMANDER+STARBASE COMBINATION FOUND--LAUNCH ATTACK
 430  BATX=BASEQX(J)
      BATY=BASEQY(J)
      RF=RANF()
      FUTURE(5)=DATE+1.0+3.0*RF
      IF(ISATB.NE.0) FUTURE(5)=FUTURE(5)+FUTURE(7)-DATE
      FUTURE(4)=FUTURE(5)+EXPRAN(0.3*INTIME)
      CH1=IHC
      ITYPE=ICHAR(CH1)
      CALL SOS
      GO TO 10
C--------EXTRANEOUS EVENT 5:  COMMANDER SUCCEEDS IN DESTROYING BASE
 500  FUTURE(5)=1E38
 502  IF(ISATB.NE.2) GO TO 505
      IF(MOD(GALAXY(ISX,ISY),100) .LT. 10)  RETURN
      IXHOLD=BATX
      IYHOLD=BATY
      BATX=ISX
      BATY=ISY
      GO TO 520
 505  IF(REMCOM.EQ.0 .OR. REMBAS.EQ.0) GO TO 515
      IF(MOD(GALAXY(BATX,BATY),100) .LT. 10) GO TO 515
      DO 510 I=1,REMCOM
      IF(CX(I).EQ.BATX .AND. CY(I).EQ.BATY) GO TO 520
 510  CONTINUE
 515  BATX=0
      BATY=0
      GO TO 10
 520  IF(STARCH(BATX,BATY) .EQ. -1) STARCH(BATX,BATY)=0
      IF(STARCH(BATX,BATY) .GT. 999)
     +   STARCH(BATX,BATY)=STARCH(BATX,BATY)-10
C--------HANDLE CASE WHERE BASE IS IN SAME QUADRANT AS STARSHIP
      IF(BATX.NE.QUADX .OR. BATY.NE.QUADY) GO TO 545
      QUAD(BASEX,BASEY)=IHDOT
      BASEX=0
      BASEY=0
      CALL NEWCON
      CALL SKIP(1)
      OLINE='SPOCK:  "CAPTAIN, I BELIEVE THE STARBASE HAS BEEN DESTROY
     +ED."'
      CALL PROUT(61)
      GO TO 550
C--------IF STARBASE NOT IN SAME QUADRANT, GET NEWS FROM UHURA
 545  IF(REMBAS.EQ.1 .OR. DAMAGE(9).GT.0) GO TO 550
      CALL SKIP(1)
      OLINE='LT. UHURA:  "CAPTAIN, STARFLEET COMMAND REPORTS THAT'
      CALL PROUT(52)
      ITEM='THE STARBASE IN'
      CALL CRAM(15)
      CALL CRAMLO(1,BATX,BATY)
      ITEM=' HAS BEEN DESTROYED BY'
      CALL CRAMDM(22)
      IF(ISATB .NE. 2) GO TO 547
      OLINE='THE KLINGON SUPER-COMMANDER."  '
      CALL PROUT(31)
      GO TO 550
 547  OLINE='A KLINGON COMMANDER."'
      CALL PROUT(21)
C--------REMOVE STARBASE FROM GALAXY
 550  GALAXY(BATX,BATY)=GALAXY(BATX,BATY)-10
      IF(REMBAS .LE. 1) GO TO 580
      DO 560 I=1,REMBAS
      IF(BASEQX(I).EQ.BATX .AND. BASEQY(I).EQ.BATY) GO TO 570
 560  CONTINUE
 570  BASEQX(I)=BASEQX(REMBAS)
      BASEQY(I)=BASEQY(REMBAS)
 580  REMBAS=REMBAS-1
      IF(ISATB .NE. 2) GO TO 515
C--------REINSTATE A COMMANDER'S BASE ATTACK.
      BATX=IXHOLD
      BATY=IYHOLD
      ISATB=0
      GO TO 10
C--------EXTRANEOUS EVENT 6:  SUPER-COMMANDER MOVES.
 600  FUTURE(6)=DATE+0.2777
      IF(IENTES+ISTRAC .GT. 0) GO TO 10
      IF(ISATB .NE. 1  .AND.  (ISCATE.NE.1 .OR. JUSTIN.EQ.1)) CALL SCOM
      GO TO 10
C--------EXTRANEOUS EVENT 7:  SUPER-COMMANDER DESTROYS BASE
 700  FUTURE(7)=1E38
      ISATB=2
      GO TO 502
C--------CHECK WITH SPY TO SEE IF S.C. SHOULD TRACTOR BEAM.
 5000 IF(NSCREM .EQ. 0) RETURN
      IF(ICTBEA+ISTRAC .GT. 0) RETURN
      IF(CONDIT.EQ.IHDOCK .OR. ISATB.EQ.1 .OR. ISCATE.EQ.1) RETURN
      IF(IENTES.NE.0) GO TO 5100
      IF((ENERGY.LT.2500.) .AND. (TORPS.LT.4) .AND. (SHLD.LT.1250.))
     +  GO TO 5100
      IF((DAMAGE(3).GT.0.) .AND. ((DAMAGE(4).GT.0) .OR.
     +   (TORPS.LT.4)))  GO TO 5100
      IF((DAMAGE(8) .GT. 0.) .AND. ((ENERGY .LT. 3000.) .OR.
     +(DAMAGE(3) .GT. 0.)) .AND. ((TORPS .LT. 5) .OR. (DAMAGE(4) .GT.
     +  0.))) GO TO 5100
      RETURN
C--------TRACTOR-BEAM HERE!
5100  IF(RANF().GT..65) RETURN
      ISTRAC=1
      GO TO 20010
      END
