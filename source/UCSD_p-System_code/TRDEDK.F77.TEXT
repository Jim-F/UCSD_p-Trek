$USES UEXPRAN IN TREK2A:TREXPR.F77
$USES UCRAMDM IN TREK2A:TRCRDM.F77
$USES UCRMSEN IN TREK2B:TRCRSE.F77
      SUBROUTINE DEADKL(IX,IY,TYPE,IXX,IYY)
$INCLUDE COMP1B1:TREKCO.F77
      CHARACTER*1 TYPE
      CHARACTER*240 ITEM
      COMMON/SCNBF1/ITEM
      EQUIVALENCE (KSTUF(1),ITHERE)
      INTRINSIC FLOAT,ICHAR
C
      CALL CRMSEN(ICHAR(TYPE),2,IXX,IYY)
C--------DECIDE WHAT KIND OF ENEMY IT IS, AND UPDATE APPROPRIATELY
      IF(TYPE .EQ. IHT) GO TO 30
      IF(TYPE .EQ. IHR) GO TO 27
      GALAXY(QUADX,QUADY)=GALAXY(QUADX,QUADY)-100
      KLHERE=KLHERE-1
      REMKL=REMKL-1
      IF(TYPE .EQ. IHK) GO TO 25
      IF(TYPE .EQ. IHS) GO TO 26
C--------CHALK UP A COMMANDER
      COMHER=0
      DO 10 I=1,REMCOM
      IF(CX(I) .EQ. QUADX .AND. CY(I) .EQ. QUADY)GO TO 15
 10   CONTINUE
 15   CX(I)=CX(REMCOM)
      CY(I)=CY(REMCOM)
      CX(REMCOM)=0
      CY(REMCOM)=0
      REMCOM=REMCOM-1
      FUTURE(2)=1E38
      IF(REMCOM.GT.0) FUTURE(2)=DATE+EXPRAN(FLOAT(INCOM/REMCOM))
      KILLC=KILLC+1
      GO TO 30
C--------CHALK UP AN ORDINARY KLINGON
 25   KILLK=KILLK+1
      GO TO 30
C--------CHALK UP THE (GULP) <SUPER-COMMANDER>.
 26   NSCREM=0
      ISHERE=0
      ISX=0
      ISY=0
      NSCKIL=1
      ISATB=0
      ISCATE=0
      FUTURE(6)=1E38
      FUTURE(7)=1E38
      GO TO 30
C--------CHALK UP A ROMULAN.
 27   NEWSTU(QUADX,QUADY)=NEWSTU(QUADX,QUADY) -10
      IRHERE=IRHERE-1
      NROMKL=NROMKL+1
      NROMRE=NROMRE-1
C--------FOR EACH KIND OF ENEMY, FINISH MESSAGE TO PLAYER
 30   ITEM=' DESTROYED. '
      CALL CRAMDM(12)
      QUAD(IX,IY)=IHDOT
      IF(REMKL .EQ. 0) RETURN
      REMTIM=REMRES/(REMKL+4*REMCOM)
C-------IF ENEMY IS A THOLIAN, SET ITHERE=0 AND RETURN
      IF(TYPE.EQ.IHT) ITHERE=0
      IF(TYPE.EQ.IHT) RETURN
C--------REMOVE ENEMY SHIP FROM ARRAYS DESCRIBING LOCAL CONDITIONS
      DO 40 I=1,NENHER
      IF(KX(I) .EQ. IX .AND. KY(I) .EQ. IY)GO TO 45
 40   CONTINUE
 45   NENHER=NENHER-1
      IF(I .GT. NENHER) GO TO 55
      DO 50 J=I,NENHER
      KX(J)=KX(J+1)
      KY(J)=KY(J+1)
      KPOWER(J)=KPOWER(J+1)
 50   KDIST(J)=KDIST(J+1)
 55   KX(NENHER+1)=0
      KY(NENHER+1)=0
      KDIST(NENHER+1)=0
      KPOWER(NENHER+1)=0
C
      RETURN
      END
