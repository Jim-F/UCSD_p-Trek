$USES UPROUT IN TREK2A:TRPROU.F77
$USES USKIP IN TREK2A:TRSKIP.F77
$USES UIRAN8 IN TREK2A:TRIRA8.F77
$USES RANDOM IN TREK2A:TRRANF.PAS
$USES UCRAM IN TREK2A:TRCRAM.F77
$USES UCREND IN TREK2A:TRCREN.F77
$USES UCRAMDM IN TREK2A:TRCRDM.F77
$USES UCRAMLO IN TREK2A:TRCRML.F77
$USES UNEWQUA IN TREK2A:TRNEWQ.F77
$USES URESETD IN TREK2A:TRRESE.F77
$USES USORTKL IN TREK2A:TRSORT.F77
$USES UCRAMF IN TREK2B:TRCRAF.F77
$USES UCRAMSH IN TREK2B:TRCRSH.F77
$USES UTIMEWR IN TREK2B:TRTIMW.F77
$USES UFINISH IN TREK2B:TRFINI.F77
$USES URAM IN TREK2B:TRRAM.F77
C
      SUBROUTINE MOVE
$INCLUDE COMP1B1:TREKCO.F77
      CHARACTER*240 OLINE
      COMMON/MYBUF/OLINE
      CHARACTER*240 ITEM
      COMMON/SCNBF1/ITEM
      INTEGER TRBEAM
      EQUIVALENCE (CRACKS(6),KDIDIT)
      CHARACTER*2 IQUAD
      INTRINSIC SIN,COS,AMAX1,ABS,SQRT,FLOAT
C
      INORBI=0
 1    ANGLE=((15.0-DIREC)*0.5235988)
      DELTAX=-SIN(ANGLE)
      DELTAY=COS(ANGLE)
      BIGGER=AMAX1(ABS(DELTAX),ABS(DELTAY))
      DELTAX=DELTAX/BIGGER
      DELTAY=DELTAY/BIGGER
      TRBEAM=0
C--------IF TRACTOR BEAM IS TO OCCUR, DO NOT MOVE FULL DISTANCE
      IF(DATE+TIME .LT. FUTURE(2)) GO TO 5
      TRBEAM=1
      CONDIT=IHRED
      DIST=DIST*(FUTURE(2)-DATE)/TIME+0.1
      TIME=FUTURE(2)-DATE + 1E-5
C--------MOVE WITHIN QUADRANT
 5    QUAD(SECTX,SECTY)=IHDOT
      X=SECTX
      Y=SECTY
      N=10.0*DIST*BIGGER+0.5
      IF(N .EQ. 0) GO TO 100
      DO 10 L=1,N
      X=X+DELTAX
      IX=X+0.5
      Y=Y+DELTAY
      IY=Y+0.5
      IF(IX .LT. 1 .OR. IX .GT. 10) GO TO 40
      IF(IY .LT. 1 .OR. IY .GT. 10) GO TO 40
      IQUAD=QUAD(IX,IY)
      IF(IQUAD .NE. IHDOT) GO TO 20
 10   CONTINUE
      DIST=0.1*SQRT(FLOAT((SECTX-IX)**2 + (SECTY-IY)**2))
      SECTX=IX
      SECTY=IY
      GO TO 100
C--------OBJECT ENCOUNTERED ALONG FLIGHT PATH
 20   STOPEG=50.0*DIST/TIME
      DIST=0.1*SQRT(FLOAT((SECTX-IX)**2 + (SECTY-IY)**2))
      IF(IQUAD.EQ.IHK .OR. IQUAD.EQ.IHC .OR. IQUAD.EQ.IHS .OR.
     +  IQUAD.EQ.IHR)  GO TO 30
      IF(IQUAD.EQ.IHT) GO TO 30
      IF(IQUAD .EQ. IHAT) GO TO 25
C--------OBJECT IS NOT AN ENEMY VESSEL, OR BLACK HOLE.
      CALL SKIP(1)
      CALL CRAMSH
      IF(IQUAD.NE.IHNUM) THEN
      ITEM=' BLOCKED BY OBJECT AT'
      CALL CRAM(21)
      ELSEIF(IQUAD.EQ.IHNUM) THEN
      ITEM=' ENCOUNTERS THOLIAN WEB AT'
      CALL CRAM(26)
      ENDIF
      CALL CRAMLO(2,IX,IY)
      ITEM=';'
      CALL CRAMDM(1)
      ITEM='EMERGENCY STOP REQUIRED '
      CALL CRAM(24)
      CALL CRAMF(STOPEG,0,2)
      ITEM=' UNITS OF ENERGY.'
      CALL CRAMDM(17)
      ENERGY=ENERGY-STOPEG
      SECTX=X-DELTAX+0.5
      SECTY=Y-DELTAY+0.5
      IF(ENERGY .GT. 0) GO TO 100
      CALL FINISH(4)
      RETURN
C--------OBJECT IS A BLACK HOLE.  SWALLOW SHIP.
 25   CALL REDALR
      CALL SKIP(1)
      CALL CRM3AS
      CALL CRAMSH
      ITEM=' PULLED INTO BLACK HOLE AT'
      CALL CRAM(26)
      CALL CRAMLO(2,IX,IY)
      CALL CREND
      IF(RANF().GT.0.50) GO TO 27
      CALL IRAN8(QUADX,QUADY)
      CALL IRAN10(SECTX,SECTY)
      OLINE='SPOCK: "CAPTAIN, INSTRUMENTS INDICATE WE HAVE UNDERGONE'
      CALL PROUT(55)
      ITEM='        A SPACE'
      CALL CRAM(15)
      XTIMEW=RANF()
      IF(XTIMEW.GT.0.65) THEN
      ITEM='-TIME'
      CALL CRAM(5)
      ENDIF
      ITEM=' PHASE SHIFT."'
      CALL CRAMDM(14)
      IF(XTIMEW.GT.0.65) CALL TIMEWR
      IF(XTIMEW.GT.0.65) KSTUF(4)=1
      GO TO 95
 27   CALL FINISH(21)
      RETURN
C--------OBJECT IS AN ENEMY VESSEL; RAM HIM.
 30   SECTX=IX
      SECTY=IY
      CALL RAM(0,IQUAD,SECTX,SECTY)
      GO TO 100
C--------COMPUTE FINAL POSITION--NEW QUADRANT, NEW SECTOR
 40   X=10*(QUADX-1)+SECTX
      Y=10*(QUADY-1)+SECTY
      IX=X+10.0*DIST*BIGGER*DELTAX+0.5
      IY=Y+10.0*DIST*BIGGER*DELTAY+0.5
C--------CHECK FOR EDGE OF GALAXY
      KINKS=0
 45   KINK=0
      IF(IX .GT. 0) GO TO 50
      IX=-IX+1
      KINK=1
 50   IF(IY .GT. 0) GO TO 55
      IY=-IY+1
      KINK=1
 55   IF(IX .LE. 80) GO TO 60
      IX=161-IX
      KINK=1
 60   IF(IY .LE. 80) GO TO 65
      IY=161-IY
      KINK=1
 65   IF(KINK .EQ. 0) GO TO 70
      KINKS=1
      GO TO 45
 70   IF(KINKS .EQ. 0) GO TO 90
      NKINKS=NKINKS+1
      IF(NKINKS .EQ. 3) GO TO 80
C--------ISSUE REPRIMAND FOR HITTING EDGE OF GALAXY
      CALL SKIP(1)
      OLINE='YOU HAVE ATTEMPTED TO CROSS THE NEGATIVE ENERGY BARRIER'
      CALL PROUT(55)
      OLINE='AT THE EDGE OF THE GALAXY.  THE THIRD TIME YOU TRY THIS,'
      CALL PROUT(56)
      OLINE='YOU WILL BE DESTROYED.'
      CALL PROUT(22)
      GO TO 90
C--------ONE, TWO, THREE STRIKES, YOU'RE OUT
 80   CALL FINISH(6)
      RETURN
C--------COMPUTE FINAL POSITION OF STARSHIP IN NEW QUADRANT
 90   CONTINUE
      QUADX=(IX+9)/10
      QUADY=(IY+9)/10
      SECTX=IX-10*(QUADX-1)
      SECTY=IY-10*(QUADY-1)
      IF(TRBEAM.NE.0) RETURN
 95   CALL SKIP(1)
      ITEM='ENTERING'
      CALL CRAM(8)
      CALL CRAMLO(1,QUADX,QUADY)
      CALL CREND
      QUAD(SECTX,SECTY)=SHIP
      CALL NEWQUA
      RETURN
C--------NO QUADRANT CHANGE; COMPUTE NEW ENEMY DISTANCES
 100  QUAD(SECTX,SECTY)=SHIP
      CALL RESETD
      IF(KDIDIT .EQ. 0) CALL SORTKL
C
      RETURN
      END
