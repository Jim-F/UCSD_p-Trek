$USES UPROUT IN TREK2A:TRPROU.F77
$USES UCHEW IN TREK2A:TRCHEW.F77
$USES USKIP IN TREK2A:TRSKIP.F77
$USES RANDOM IN TREK2A:TRRANF.PAS
$USES UPROMPT IN TREK2A:TRPROM.F77
$USES USCAN IN TREK2A:TRSCAN.F77
$USES UCRAM IN TREK2A:TRCRAM.F77
$USES UCRAMI IN TREK2A:TRCRAI.F77
$USES UCREND IN TREK2A:TRCREN.F77
$USES UCRAMDM IN TREK2A:TRCRDM.F77
$USES UCRAMLO IN TREK2A:TRCRML.F77
$USES UDROPIN IN TREK2A:TRDROP.F77
$USES URESETD IN TREK2A:TRRESE.F77
$USES USORTKL IN TREK2A:TRSORT.F77
$USES UNEWCON IN TREK2A:TRNEWC.F77
$USES UCRAMF IN TREK2B:TRCRAF.F77
$USES UCRAMSH IN TREK2B:TRCRSH.F77
$USES UCASULT IN TREK2B:TRCASU.F77
$USES UFINISH IN TREK2B:TRFINI.F77
$USES UDEADKL IN TREK2B:TRDEDK.F77
$USES UCRMSEN IN TREK2B:TRCRSE.F77
$USES UZAP IN TREK2B:TRZAP.F77
$USES UNOVA IN TREK2B:TRNOVA.F77
C
      SUBROUTINE PHOTON(KLINGO)
C
C       13-JUN-79
C       CORRECT 'THOLIAN DISAPPEARS' MESSAGE
C       DON'T CHARGE PLAYER FOR A PLANET DESTROYED BY AN ENEMY
C
$INCLUDE COMP1B2:TREKCO.F77
      CHARACTER*240 OLINE
      COMMON/MYBUF/OLINE
      CHARACTER*240 ITEM
      CHARACTER*2 KEY
      REAL FITEM
      COMMON/SCANBF/KEY
      COMMON/SCNBF1/ITEM
      COMMON/SCNBF2/FITEM
      DIMENSION TARG(3,2), COURSE(3)
      REAL KP
      INTEGER BOT,TOP,TOP2
      CHARACTER*1 IQUAD,CH1
      EQUIVALENCE (CRACKS(1),HIT),(CRACKS(4),KSHOT)
      EQUIVALENCE (KSTUF(1),ITHERE),(KSTUF(2),ITHX),(KSTUF(3),ITHY)
      INTRINSIC SIN,COS,AMAX1,AMIN1,ATAN2,ABS,ICHAR,SIGN,SQRT
      CHARACTER*2 IHDOC
      DATA IHDOC/'DO'/
C
      IF (KLINGO.EQ.1) GO TO 6000
C
C--------CHECK TORPEDO COUNT AND STATUS OF PHOTON TUBES
      IDIDIT=1
      KSHOT=0
      IF(DAMAGE(4) .EQ. 0)GO TO 10
      OLINE='PHOTON TUBES DAMAGED.'
      CALL PROUT(21)
      GO TO 3005
 10   IF(TORPS .NE.0)GO TO 15
      OLINE='NO TORPEDOES LEFT.'
      CALL PROUT(18)
      GO TO 3005
C--------SCAN AND MAKE SENSE OF COMMAND INPUT LINE
 15   CALL SCAN
      TEMP=FITEM
      N = TEMP + 0.5
      IF(KEY .EQ. IHREAL) GO TO 30
 16   CALL CRAMI(TORPS,0)
      ITEM=' TORPEDOES LEFT.'
      CALL CRAMDM(16)
      OLINE='NUMBER OF TORPEDOES TO FIRE:  '
      CALL PROMPT(30)
      GO TO 15
 30   IF(N .LE. 0) GO TO 3005
      IF(N .LE. 3) GO TO 31
      OLINE='MAXIMUM OF 3 TORPEDOES PER BURST.'
      CALL PROUT(33)
      GO TO 16
 3004 CALL BEGPAR
 3005 IDIDIT=0
      RETURN
 31   IF(N .LE. TORPS) GO TO 32
      GO TO 16
C--------ALL TORPEDOES FIRED AT SAME SPOT.
 3101 DO 3102 I=2,N
      TARG(I,1)=TARG(1,1)
 3102 TARG(I,2)=TARG(1,2)
      GO TO 36
C--------READ IN TARGET SECTORS
 32   DO 33 I=1,N
      CALL SCAN
      TARG(I,1)=FITEM
      IF((I.EQ.1) .AND. (KEY.EQ.IHEOL)) GO TO 34
      IF((I .EQ. 2) .AND. (KEY .EQ. IHEOL))  GO TO 3101
      IF (KEY .NE. IHREAL) GO TO 3004
      CALL SCAN
      TARG(I,2)=FITEM
 33   IF (KEY .NE. IHREAL) GO TO 3004
      GO TO 36
 34   DO 35 I=1,N
      CALL CHEW
      ITEM='TARGET SECTOR FOR TORPEDO NUMBER '
      CALL CRAM (33)
      CALL CRAMI(I,0)
      ITEM=':  '
      CALL CRAM(3)
      CALL CRENDN
      CALL SCAN
      TARG(I,1)=FITEM
      IF (KEY .NE. IHREAL) GO TO 3004
      CALL SCAN
      TARG(I,2)=FITEM
 35   IF( KEY .NE. IHREAL) GO TO 3004
C--------CHECK FOR INVALID TARGETS.
 36   DO 37 I=1,N
      DO 37 J=1,2
      TEMP = TARG(I,J)
      IF(TEMP.EQ.-1.) GO TO 3005
 37   IF ((TEMP .LT. 0.999) .OR. (TEMP .GT. 10.001)) GO TO 3004
C--------COMPUTE COURSE FOR EACH TORPEDO.
      DO 40 I=1,N
      DELTX = 0.1*(TARG(I,2)-SECTY)
      DELTY=0.1*(SECTX-TARG(I,1))
C--------DO NOT ALLOW ENTERPRISE TO SHOOT A TORPEDO AT ITSELF.
      IF((DELTX .NE. 0) .OR. (DELTY .NE. 0)) GO TO 40
      CALL SKIP(1)
      OLINE='SPOCK:  "BRIDGE TO SICKBAY.  DR. MC COY, '
      CALL PROUT(41)
      OLINE='  I RECOMMEND AN IMMEDIATE REVIEW OF'
      CALL PROUT(36)
      OLINE='  THE CAPTAIN''S PSYCHOLOGICAL PROFILE."  '
      CALL PROUT(41)
      GO TO 3005
 40   COURSE (I) = 1.90985932*ATAN2(DELTX,DELTY)
C--------BEGIN OUTER LOOP FOR MOVING <N> TORPEDOES
      I=0
 50   I=I+1
      IF(I .GT. N) GO TO 115
      IF(CONDIT .NE. IHDOC ) TORPS=TORPS-1
      J=I
      STARTX=SECTX
      STARTY=SECTY
      R=(RANF()+RANF())*0.5 -0.5
      IF(ABS(R) .LT. 0.49) GO TO 5709
      R=(RANF()+1.2)*R
C-------- TORPEDO MISFIRES
      IF(N.GT.1) GO TO 5706
      OLINE='***TORPEDO MISFIRES. '
      CALL PROUT(21)
      GO TO 5707
 5706 ITEM='***TORPEDO NUMBER '
      CALL CRAM(18)
      CALL CRAMI(I,0)
      ITEM=' MISFIRES.'
      CALL CRAMDM(10)
      IF(I . LT.  N) THEN
      OLINE='   REMAINDER OF BURST ABORTED. '
      CALL PROUT(31)
      ENDIF
      I=N
 5707 IF(RANF() .GT. .200000)GO TO 5709
C------- CHECK IF MISFIRE DOES SHIP DAMAGE. IF SO DON'T GIVE TRACK.
      OLINE='***PHOTON TUBES DAMAGED BY MISFIRE.'
      CALL PROUT(35)
      DAMAGE(4)=DAMFAC*(1.0+2.0*RANF())
      GO TO 115
 5709 IF(SHLDUP.NE.0 .OR. CONDIT.EQ.IHDOC ) R=R+0.001*SHLD*R
      AC=COURSE(I)+0.25*R
      GO TO 5710
C*
C     ENTRY KPHOTON
C*
 6000 CONTINUE
C
C----------- SET FLAG FOR KLINGON
C--------ENEMY FIRES PHOTON TORPEDO
      IX=KX(KSHOT)
      IY=KY(KSHOT)
      I=1
      N=1
      STARTX=IX
      STARTY=IY
      DELTX=0.1*(SECTY-STARTY)
      DELTY=0.1*(STARTX-SECTX)
      AC=1.90985932*ATAN2(DELTX,DELTY)
      TEMP=RANF()-0.5
      R=TEMP*(1.0+0.001*KPOWER(KSHOT))+RANF()*TEMP
      AC=AC+0.25*R
      IQUAD=QUAD(IX,IY)
      CALL CRMSEN(ICHAR(IQUAD),0,IX,IY)
      ITEM=' FIRES PHOTON TORPEDO.'
      CALL CRAMDM(22)
C--------CHECK FOR KLINGON MISFIRE
      IF(ABS(TEMP) .LT. 0.45) GO TO 5710
C--------ENEMY TORPEDO MISFIRED.  DETERMINE DAMAGE.
      ANGLE=(15.0-AC)*0.5235988
      HIT=200.0 + 600.0*RANF()
      LL=KSHOT
      OLINE='***TORPEDO MISFIRES! '
      CALL PROUT(21)
      GO TO 68
 5710 ANGLE=(15.0-AC)*0.5235988
      IF(N .EQ. 1)GO TO  58
      CALL SKIP(1)
      ITEM='TRACK FOR TORPEDO NUMBER '
      CALL CRAM(25)
      CALL CRAMI(J,0)
 5720 ITEM=':     '
      CALL CRAM(6)
      GO TO 59
 58   CALL SKIP(1)
      ITEM='TORPEDO TRACK: '
      CALL CRAM(15)
 59   DELTAX=-SIN(ANGLE)
      DELTAY=COS(ANGLE)
      BIGGER=AMAX1(ABS(DELTAX),ABS(DELTAY))
      DELTAX=DELTAX/BIGGER
      DELTAY=DELTAY/BIGGER
      X=STARTX
      Y=STARTY
C--------BEGIN INNER LOOP FOR MOVING A SINGLE TORPEDO
      L=0
 5910 L=L+1
      X=X+DELTAX
      IX=X+0.5
      IF(IX .LT. 1 .OR. IX .GT. 10)GO TO 105
      Y=Y+DELTAY
      IY=Y+0.5
      IF(IY .LT. 1 .OR. IY .GT. 10)GO TO 105
      IF(L .EQ. 4 .OR. L .EQ. 9)CALL CREND
      CALL CRAMF(X,0,1)
      ITEM=' - '
      CALL CRAM(3)
      CALL CRAMF(Y,0,1)
      ITEM='   '
      CALL CRAM(3)
      IQUAD=QUAD(IX,IY)
      IF(IQUAD.EQ.IHDOT) GOTO 5910
C--------BEGIN HIT CHECKS
      CALL CREND
C--------COMPUTE EXACT ANGLE TO SECTOR HIT, DISTANCE TO SECTOR HIT,
C        NORMAL MISS DISTANCE, AND HIT
      DIST=SQRT((STARTX-IX)**2 + (STARTY-IY)**2)
      DELTX=0.1*(IY-STARTY)
      DELTY=0.1*(STARTX-IX)
      BULSEY=1.90985932*ATAN2(DELTX,DELTY)
      BULSEY=(15.0-BULSEY)*0.5235988
      SINANG=SIN(ANGLE-BULSEY)
      DISTN=ABS(SINANG) * DIST
      HIT=700.0+100.0*RANF()-1000.0*DISTN
C--------TEST FOR AND COPE WITH HIT ON KLINGON,ROMULAN, OR COMMANDER.
      IF(IQUAD .EQ. IHK  .OR.  IQUAD .EQ. IHR) GO TO 62
      IF(IQUAD .NE. IHC  .AND.  IQUAD .NE. IHS) GO TO 70
      IF(RANF() .GT. 0.05) GO TO 62
      CALL CRMSEN(ICHAR(IQUAD),2,IX,IY)
      ITEM=' USES ANTI-PHOTON DEVICE;'
      CALL CRAMDM(25)
      OLINE='   TORPEDO NEUTRALIZED.'
      CALL PROUT(23)
      GO TO 50
 62   DO 65 LL=1,NENHER
      IF(IX.EQ.KX(LL) .AND. IY.EQ.KY(LL)) GO TO 68
 65   CONTINUE
 68   KP=KPOWER(LL)
      KPOWER(LL)=KP-SIGN(AMIN1(ABS(KP),HIT),KP)
      IF(KPOWER(LL) .NE. 0.0) GO TO 69
      CALL DEADKL(IX,IY,IQUAD,IX,IY)
      GO TO 50
 69   CALL CRMSEN(ICHAR(IQUAD),2,IX,IY)
C--------IF ENEMY DAMAGED BUT NOT DESTROYED, TRY TO DISPLACE HIM
 6901 ANG=ANGLE + 2.5*(RANF()-0.5)
      TEMP=AMAX1(ABS(-SIN(ANG)),ABS(COS(ANG)))
      XX=-SIN(ANG) / TEMP
      YY= COS(ANG) / TEMP
      JX=IX+XX+0.5
      JY=IY+YY+0.5
      IF(JX.LT.1 .OR. JX.GT.10 .OR. JY.LT.1 .OR. JY.GT.10) GO TO 6905
      IF(QUAD(JX,JY) .NE. IHAT) GO TO 6903
C--------SOMEONE FALLS INTO A BLACK HOLE
      ITEM=' BUFFETED INTO BLACK HOLE.'
      CALL CRAMDM(26)
      IF(IQUAD .NE.SHIP) GO TO 6902
      CALL FINISH(21)
      RETURN
 6902 CALL DEADKL(IX,IY,IQUAD,JX,JY)
      GO TO 50
 6903 IF(QUAD(JX,JY) .NE. IHDOT) GO TO 6905
      QUAD(JX,JY)=IQUAD
      QUAD(IX,IY)=IHDOT
      ITEM=' DAMAGED--'
      CALL CRAMDM(10)
      ITEM='   DISPLACED BY BLAST TO'
      CALL CRAM(24)
      CALL CRAMLO(2,JX,JY)
      CALL CREND
      IF(IQUAD .NE.SHIP) GO TO 6904
C------STARSHIP DISPLACED BY TORPEDO.  MOVE IT AND RESET ENEMY DISTANCES
      SECTX=JX
      SECTY=JY
      CALL RESETD
      GO TO 50
C--------ENEMY DISPLACED BY PHOTON BLAST
 6904 KX(LL)=JX
      KY(LL)=JY
      KDIST(LL)=          SQRT(FLOAT((SECTX-JX)**2 + (SECTY-JY)**2))
      GO TO 50
 6905 ITEM=' DAMAGED BUT NOT DESTROYED.'
      CALL CRAMDM(27)
      GO TO 50
C--------TEST FOR AND COPE WITH HIT ON A STARBASE
 70   IF(IQUAD.NE.IHT) GO TO  7002
      CH1=IHT
      CALL CRMSEN(ICHAR(CH1),2,IX,IY)
      IF(HIT.LT.600) GO TO 7001
      ITEM=' DESTROYED.'
      CALL CRAMDM(11)
      QUAD(IX,IY)=IHDOT
      ITHERE=0
      ITHX=0
      ITHY=0
      GO TO 50
 7001 X=RANF()
      IF(X.GT.0.05) THEN
      ITEM=' SURVIVES PHOTON BLAST.'
      CALL CRAMDM(23)
      ENDIF
      IF(X.GT.0.05) GO TO 50
      ITEM=' DISAPPEARS.'
      CALL CRAMDM(12)
      QUAD(IX,IY)=IHNUM
      CALL DROPIN(IHAT,IDUM,MY)
      ITHERE=0
      GO TO 50
 7002 IF(IQUAD.NE.IHNUM) GOTO 7005
      OLINE='***TORPEDO ABSORBED BY THOLIAN WEB.'
      CALL PROUT(35)
      GOTO 50
 7005 IF(IQUAD.NE.IHB) GO TO 75
      CH1=IHB
      CALL CRMSEN(ICHAR(CH1),2,IX,IY)
      IF(HIT .LT. 600.0) GO TO 7510
      ITEM=' DESTROYED  '
      CALL CRAM(12)
      IF(IPHWHO.NE.1) THEN
      ITEM='.... CONGRATULATIONS '
      CALL CRAM(21)
      ENDIF
      CALL CREND
      IF(STARCH(QUADX,QUADY) .LT. 0) STARCH(QUADX,QUADY)=0
      DO 71 LLL=1,REMBAS
      IF(BASEQX(LLL).NE.QUADX .OR. BASEQY(LLL).NE.QUADY) GO TO 71
      BASEQX(LLL)=BASEQX(REMBAS)
      BASEQY(LLL)=BASEQY(REMBAS)
 71   CONTINUE
      QUAD(IX,IY)=IHDOT
      REMBAS=REMBAS-1
      BASEX=0
      BASEY=0
      GALAXY(QUADX,QUADY)=GALAXY(QUADX,QUADY)-10
      IF(IPHWHO.NE.1) BASEKL=BASEKL+1
      CALL NEWCON
      GO TO 50
C--------TEST FOR AND COPE WITH A HIT ON A PLANET.
 75   IF(IQUAD .NE. IHP) GO TO 80
      CH1=IHP
      CALL CRMSEN(ICHAR(CH1),2,IX,IY)
      IF(HIT .GT. 450.0) GO TO 7520
 7510 ITEM=' SURVIVES PHOTON BLAST.'
      CALL CRAMDM(23)
      GO TO 50
 7520 ITEM=' DESTROYED.'
      CALL CRAMDM(11)
      IF(IPHWHO.NE.1)NPLANK=NPLANK+1
      NEWSTU(QUADX,QUADY)=NEWSTU(QUADX,QUADY)-1
      DO 76 II=1,5
 76   PLNETS(IPLANE,II)=0
      IPLANE=0
      PLNETX=0
      PLNETY=0
      QUAD(IX,IY)=IHDOT
      IF(LANDED .NE. 1) GO TO 50
C--------CAPTAIN PERISHES ON PLANET.
      CALL FINISH(15)
      RETURN
 80   IF(IQUAD .NE. IHSTAR) GO TO 90
C--------STAR HIT - CHECK FOR NOVA RESULTS
      IF(HIT.GT.270.0) GO TO 85
      CH1=IHSTAR
      CALL CRMSEN(ICHAR(CH1),2,IX,IY)
      ITEM=' UNAFFECTED BY PHOTON BLAST.'
      CALL CRAMDM(28)
      GO TO 50
 85   CALL NOVA(IX,IY)
      IF(GALAXY(QUADX,QUADY) .EQ. 1000) RETURN
      IF(ALLDON.NE.0) RETURN
      GO TO 50
C--------CHECK FOR A BOOMERANG TORPEDO
 90   IF(IQUAD .NE.SHIP) GO TO 93
      IF(CONDIT.NE.IHDOC) GO TO 9010
      IF(RANF().GT.0.77) GO TO 9010
      ITEM=' STAR BASE SHIELDS PROTECT '
      CALL CRAM(27)
      CALL CRAMSH
      CALL CREND
      GO TO 50
 9010 CALL CRM3AS
      CALL CRAMSH
      ITEM=' BLASTED BY PHOTON TORPEDO!'
      CALL CRAMDM(27)
      CALL NEWCON
      CALL ZAP
      IF(ENERGY .GT. 0.0) GO TO 91
      IF(KSHOT.NE.0) GO TO 9020
      CALL FINISH(22)
      RETURN
 9020 CALL FINISH(5)
      RETURN
 91   CALL CASULT
C--------CHECK TO SEE IF SHIP DISPLACED
      CALL CRAMSH
      GO TO 6901
C--------CHECK FOR HIT ON THING.
 93   IF(IQUAD .NE. IHQUES) GO TO 95
      CALL SKIP(1)
      OLINE='"AAAAIIIIIEEEEEEEEEAAAAAAAAUUUUUGGGGGHHHHHHHHHHHHHHH!!!'
      CALL PROUT(55)
      OLINE='    HACK!     HACK!     HACK!         *CHOKE!* "'
      CALL PROUT(48)
      CALL SKIP(1)
      OLINE='MR. SPOCK:  "FASCINATING!"'
      CALL PROUT(26)
      QUAD(IX,IY)=IHDOT
      GO TO 50
C--------TORPEDO ENTERS VICINITY OF BLACK HOLE.  CHECK FOR DEFLECTION
 95   CH1=IHAT
      CALL CRMSEN(ICHAR(CH1),2,IX,IY)
      IF(DISTN .LT. 0.1) GO TO 97
C--------BOING.  COMPUTE DEFLECTION ANGLE, AND NEW STARTING POINT
      SPRANG=(0.5-DISTN) * 7.853981634
      BETA=1.57079633 + ANGLE - SPRANG
      STARTX = IX-DISTN*SIN(BETA)
      STARTY = IY+DISTN*COS(BETA)
      ANGLE = ANGLE - SIGN(SPRANG,SINANG)
      ITEM=' DEFLECTS TORPEDO.'
      CALL CRAMDM(18)
      ITEM='NEW TRACK'
      CALL CRAM(9)
      GO TO 5720
 97   ITEM=' SWALLOWS TORPEDO.'
      CALL CRAMDM(18)
      GO TO 50
C--------END INNER LOOP FOR MOVING ONE TORPEDO
 105  CALL CREND
 106  OLINE='TORPEDO MISSED.'
      CALL PROUT(15)
      GO TO 50
 115  IF(REMKL .EQ. 0) CALL FINISH(1)
      IF(KSHOT .EQ. 0) CALL SORTKL
C
      RETURN
      END
