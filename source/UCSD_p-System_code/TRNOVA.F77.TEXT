$USES UPROUT IN TREK2A:TRPROU.F77
$USES USKIP IN TREK2A:TRSKIP.F77
$USES RANDOM IN TREK2A:TRRANF.PAS
$USES UCRAM IN TREK2A:TRCRAM.F77
$USES UCREND IN TREK2A:TRCREN.F77
$USES UCRAMDM IN TREK2A:TRCRDM.F77
$USES UCRAMLO IN TREK2A:TRCRML.F77
$USES UNEWCON IN TREK2A:TRNEWC.F77
$USES UCRAMF IN TREK2B:TRCRAF.F77
$USES UCRAMSH IN TREK2B:TRCRSH.F77
$USES UFINISH IN TREK2B:TRFINI.F77
$USES UDEADKL IN TREK2B:TRDEDK.F77
$USES UCRMSEN IN TREK2B:TRCRSE.F77
$USES UMOVE IN TREK2B:TRMOVE.F77
$USES UZAP IN TREK2B:TRZAP.F77
$USES USNOVA IN TREK2B:TRSNOV.F77
C
      SUBROUTINE NOVA(IX,IY)
C
C       5-DEC-79
C       DON'T CHARGE PLAYER FOR A PLANET NOVAED BY AN ENEMY
C
$INCLUDE COMP1B2:TREKCO.F77
      CHARACTER*240 OLINE
      COMMON/MYBUF/OLINE
      CHARACTER*240 ITEM
      COMMON/SCNBF1/ITEM
      CHARACTER*1 IQUAD,IQUAD1,CH1
      INTEGER BURST,HITS(10,2),BOT,TOP,TOP2
      EQUIVALENCE (CRACKS(1),HIT),(CRACKS(4),KSHOT)
      INTRINSIC ICHAR,SQRT,FLOAT,ISIGN
      REAL COURSE(9)
      DATA COURSE/ 10.5, 12.0, 1.5, 9.0, 0.0, 3.0, 7.5, 6.0, 4.5 /
C--------CHECK FOR SUPERNOVA POSSIBILITY
      IF(RANF() .GE. 0.05) GO TO 76
      CALL SNOVA(IX,IY)
      RETURN
C--------PRINT NOVA MESSAGE FOR INITIAL STAR AT LOCATION (IX,IY)
 76   QUAD(IX,IY)=IHDOT
      CH1=IHSTAR
      CALL CRMSEN(ICHAR(CH1),2,IX,IY)
      ITEM=' NOVAS.'
      CALL CRAMDM(7)
      GALAXY(QUADX,QUADY)=GALAXY(QUADX,QUADY)-1
C-------IF ENTERPRISE DESTROYS STAR, TAKE OFF POINTS
      IF(IPHWHO.NE.1) STARKL=STARKL+1
C--------SET UP STACK TO RECURSIVELY TRIGGER ADJACENT STARS
      BOT=1
      TOP=1
      TOP2=1
      KOUNT=0
      ICX=0
      ICY=0
      HITS(BOT,1)=IX
      HITS(BOT,2)=IY
 78   DO 90 MM=BOT,TOP
      DO 90 NN=1,3
      DO 90 J=1,3
      IF((J*NN) .EQ. 4)GO TO 90
      II=HITS(MM,1)+NN-2
      JJ=HITS(MM,2)+J-2
      IF(II .LT. 1 .OR. II .GT. 10)GO TO 90
      IF(JJ .LT. 1 .OR. JJ .GT. 10)GO TO 90
      IQUAD=QUAD(II,JJ)
      IF(IQUAD.EQ.IHDOT .OR. IQUAD.EQ.IHQUES .OR. IQUAD.EQ.IHAT)
     +  GO TO 90
      IF(IQUAD.EQ.IHNUM) GO TO 90
      IF(IQUAD.EQ.IHT) GO TO 90
      IF(IQUAD .NE. IHSTAR) GO TO 80
C--------ANOTHER STAR AFFECTED BY A NOVA
      IF(RANF() .GE. .05)GO TO 79
      CALL SNOVA(II,JJ)
      RETURN
 79   TOP2=TOP2+1
      HITS(TOP2,1)=II
      HITS(TOP2,2)=JJ
      GALAXY(QUADX,QUADY)=GALAXY(QUADX,QUADY)-1
      IF(IPHWHO.NE.1) STARKL=STARKL+1
      CH1=IHSTAR
      CALL CRMSEN(ICHAR(CH1),2,II,JJ)
      ITEM=' NOVAS.'
      CALL CRAM(7)
      GO TO 8905
 80   IF(IQUAD .NE. IHP) GO TO 8002
C--------PLANET DESTROYED BY NOVA.
      NEWSTU(QUADX,QUADY)=NEWSTU(QUADX,QUADY) -1
      IF(IPHWHO.NE.1)NPLANK=NPLANK+1
      CH1=IHP
      CALL CRMSEN(ICHAR(CH1),2,II,JJ)
      ITEM=' DESTROYED.'
      CALL CRAM(11)
      DO 8001 I=1,5
 8001 PLNETS(IPLANE,I)=0
      IPLANE=0
      PLNETX=0
      PLNETY=0
      IF(LANDED .NE. 1) GO TO 8905
      CALL FINISH(0)
      GO TO 95
 8002 IF(IQUAD .NE. IHB) GO TO 82
C----------NOVA DESTROYS STARBASE
      GALAXY(QUADX,QUADY)=GALAXY(QUADX,QUADY)-10
      DO 81 LLL=1,REMBAS
      IF(BASEQX(LLL).NE.QUADX .OR. BASEQY(LLL).NE.QUADY) GO TO 81
      BASEQX(LLL)=BASEQX(REMBAS)
      BASEQY(LLL)=BASEQY(REMBAS)
 81   CONTINUE
      REMBAS=REMBAS-1
      BASEX=0
      BASEY=0
      IF(IPHWHO.NE.1) BASEKL=BASEKL+1
      CALL NEWCON
      CH1=IHB
      CALL CRMSEN(ICHAR(CH1),2,II,JJ)
      ITEM=' DESTROYED. '
      CALL CRAM(12)
      GO TO 8905
 82   HIT=800.0 + 800.0*RANF()
      IF(IQUAD .NE.SHIP) GO TO 87
C----------STARSHIP IN A NOVA
      OLINE='***STARSHIP BUFFETED BY NOVA.'
      CALL PROUT(29)
      KSHOT=0
      CALL ZAP
C-------CHECK IF STARSHIP SURVIVED NOVA
      IF(ENERGY .GT. 0)GO TO 86
      CALL FINISH(7)
      RETURN
C--------ADD IN COURSE NOVA CONTRIBUTES TO KICKING STARSHIP
 86   ICX=ICX+SECTX-HITS(MM,1)
      ICY=ICY+SECTY-HITS(MM,2)
      KOUNT=KOUNT+1
      GO TO 90
C--------ENEMY DESTROYED OR DAMAGED ; BUFFETED BY NOVA.
 87   IF(IQUAD .EQ. IHK) GO TO 88
      DO 8701 LL=1,NENHER
      IF(KX(LL).EQ.II .AND. KY(LL).EQ.JJ) GO TO 8702
 8701 CONTINUE
 8702 KPOWER(LL)=KPOWER(LL)-HIT
      IF(KPOWER(LL) .LE. 0) GO TO 88
      NEWCX=II+II-HITS(MM,1)
      NEWCY=JJ+JJ-HITS(MM,2)
      CALL CRMSEN(ICHAR(IQUAD),2,II,JJ)
      ITEM=' DAMAGED'
      CALL CRAM(8)
      IF(NEWCX.LT.1 .OR. NEWCX.GT.10 .OR.
     +   NEWCY.LT.1 .OR. NEWCY.GT.10) GO TO 8703
      IQUAD1=QUAD(NEWCX,NEWCY)
      IF(IQUAD1 .NE. IHAT) GO TO 9702
C--------ENEMY DISPLACED INTO BLACK HOLE
      ITEM=', BLASTED INTO BLACK HOLE.'
      CALL CRAMDM(26)
      CALL DEADKL(II,JJ,IQUAD,NEWCX,NEWCY)
      GO TO 90
 9702 IF(IQUAD1 .NE. IHDOT) GO TO 8703
      ITEM=' BUFFETED TO '
      CALL CRAM(13)
      CALL CRAMLO(2,NEWCX,NEWCY)
      QUAD(II,JJ)=IHDOT
      QUAD(NEWCX,NEWCY)=IQUAD
      KX(LL)=NEWCX
      KY(LL)=NEWCY
      KDIST(LL)=SQRT(FLOAT((SECTX-NEWCX)**2+(SECTY-NEWCY)**2))
 8703 CALL CREND
      GO TO 90
C--------ENEMY DESTROYED BY NOVA.
 88   CALL DEADKL(II,JJ,IQUAD,II,JJ)
      GO TO 90
 8905 CALL CREND
      QUAD(II,JJ)=IHDOT
 90   CONTINUE
C--------IF MORE STARS AFFECTED BY NOVA GO FIND WHAT THEY GOT
      IF(TOP .EQ. TOP2)GO TO 93
      BOT=TOP+1
      TOP=TOP2
      GO TO 78
 93   IF(KOUNT .EQ. 0)RETURN
C--------STARSHIP AFFECTED BY NOVA - KICK IT AWAY.
      DIST=KOUNT*.1
      IF(ICX .NE. 0) ICX=ISIGN(1,ICX)
      IF(ICY .NE. 0) ICY=ISIGN(1,ICY)
      INDEX=3*(ICX+1)+ICY+2
      DIREC=COURSE(INDEX)
      IF(DIREC .EQ. 0) DIST=0
      IF(DIST .EQ. 0)RETURN
      TIME=12.0*DIST
      CALL SKIP(1)
      OLINE='FORCE OF NOVA DISPLACES STARSHIP. '
      CALL PROUT(34)
      CALL MOVE
C
 95   RETURN
      END
