$USES UPROUT IN TREK2A:TRPROU.F77
$USES USKIP IN TREK2A:TRSKIP.F77
$USES RANDOM IN TREK2A:TRRANF.PAS
$USES UCRAM IN TREK2A:TRCRAM.F77
$USES UCRAMDM IN TREK2A:TRCRDM.F77
$USES UCRAMLO IN TREK2A:TRCRML.F77
$USES UNEWQUA IN TREK2A:TRNEWQ.F77
$USES URESETD IN TREK2A:TRRESE.F77
$USES USORTKL IN TREK2A:TRSORT.F77
$USES UCRAMSH IN TREK2B:TRCRSH.F77
$USES UFINISH IN TREK2B:TRFINI.F77
$USES UDOCK IN TREK2B:TRDOCK.F77
C
      SUBROUTINE HELP
C
C       4-DEC-79
C       SET DISTANCES CORRECTLY WHEN SHIP MATERIALIZES
C
$INCLUDE COMP1B2:TREKCO.F77
      CHARACTER*240 OLINE
      COMMON/MYBUF/OLINE
      CHARACTER*240 ITEM
      COMMON/SCNBF1/ITEM
      INTEGER SHUTUP
      EQUIVALENCE (CRACKS(2),SHUTUP)
      INTRINSIC SQRT,FLOAT
C--------TEST FOR CONDITIONS WHICH PREVENT CALLING FOR HELP
      IF(CONDIT .NE. IHDOCK ) GO TO 10
      OLINE='LT. UHURA:  "BUT CAPTAIN, WE''RE ALREADY DOCKED."'
      CALL PROUT(48)
      RETURN
 10   IF(DAMAGE(9) .EQ. 0) GO TO 20
      OLINE='SUBSPACE RADIO DAMAGED.'
      CALL PROUT(23)
      RETURN
 20   IF(REMBAS .NE. 0) GO TO 30
      OLINE='LT. UHURA:  "CAPTAIN, I''M NOT GETTING ANY RESPONSE FROM
     + STARBASE."'
      CALL PROUT(66)
      RETURN
 30   IF(LANDED .NE. 1) GO TO 31
      ITEM='YOU MUST BE ABOARD THE '
      CALL CRAM(23)
      CALL CRAMSH
      ITEM='.'
      CALL CRAMDM(1)
      RETURN
 31   IF(ISCRAF .NE. 0) GO TO 32
      OLINE='YOU MAY NOT LEAVE SHUTTLE CRAFT ON PLANET.'
      CALL PROUT(42)
      RETURN
C--------DETERMINE APPROXIMATE DISTANCE TO NEAREST STARBASE
 32   NHELP=NHELP+1
      IF(BASEX .EQ. 0) GO TO 40
      DIST=SQRT(FLOAT((BASEX-SECTX)**2 + (BASEY-SECTY)**2))
      GO TO 60
 40   DIST=1E38
      DO 50 L=1,REMBAS
      XDIST=10.0*SQRT(FLOAT((BASEQX(L)-QUADX)**2+(BASEQY(L)-QUADY)**2))
      IF(XDIST .GT. DIST) GO TO 50
      DIST=XDIST
      LINE=L
 50   CONTINUE
C--------IF STARBASE IS NOT IN THIS QUADRANT, SET UP NEW QUADRANT
      QUADX=BASEQX(LINE)
      QUADY=BASEQY(LINE)
      SHUTUP=1
      CALL NEWQUA
      SHUTUP=0
C--------DEMATERIALIZE STARSHIP
 60   QUAD(SECTX,SECTY)=IHDOT
      ITEM='STARBASE IN'
      CALL CRAM(11)
      CALL CRAMLO(1,QUADX,QUADY)
      ITEM=' RESPONDS--'
      CALL CRAM(11)
      CALL CRAMSH
      ITEM=' DEMATERIALIZES.'
      CALL CRAMDM(16)
C--------GIVE STARBASE THREE CHANCES TO REMATERIALIZE STARSHIP
      PROBF=(1.0 - 0.98**DIST)**0.3333333333
      DO 80 L=1,3
      IF(L .EQ. 1) THEN
      ITEM='1ST '
      CALL CRAM(4)
      ENDIF
      IF(L .EQ. 2) THEN
      ITEM='2ND '
      CALL CRAM(4)
      ENDIF
      IF(L .EQ. 3) THEN
      ITEM='3RD '
      CALL CRAM(4)
      ENDIF
      ITEM='ATTEMPT TO RE-MATERIALIZE '
      CALL CRAM(26)
      CALL CRAMSH
      ITEM=' . . . . . '
      CALL CRAM(11)
      IF(RANF() .GT. PROBF) GO TO 90
 70   ITEM='FAILS.'
      CALL CRAMDM(6)
 80   CONTINUE
C--------ONE, TWO, THREE STRIKES YOU'RE OUT
      CALL FINISH(11)
      RETURN
C--------REMATERIALIZATION ATTEMPT SHOULD SUCCEED, IF CAN GET ADJ TO BASE
 90   DO 100 LL=1,5
      RF=RANF()
      IX=BASEX+IFIX(3.0*RF)-1
      IF(IX.EQ.0 .OR. IX.EQ.11) GO TO 100
      RF=RANF()
      IY=BASEY+IFIX(3.0*RF)-1
      IF(IY.EQ.0 .OR. IY.EQ.11) GO TO 100
      IF(QUAD(IX,IY) .EQ. IHDOT) GO TO 110
 100  CONTINUE
      ITEM='FAILS.'
      CALL CRAMDM(6)
      CALL FINISH(11)
      RETURN
C--------ATTEMPT HAS SUCCEEDED--FINISH UP
 110  ITEM='SUCCEEDS.'
      CALL CRAMDM(9)
      SECTX=IX
      SECTY=IY
      QUAD(IX,IY)=SHIP
      CALL RESETD
      CALL SORTKL
      CALL DOCK
      CALL SKIP(1)
      OLINE='LT. UHURA:  "CAPTAIN, WE MADE IT!"'
      CALL PROUT(34)
C
      RETURN
      END
