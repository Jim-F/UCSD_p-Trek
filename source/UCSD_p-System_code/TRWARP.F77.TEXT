$USES UPROUT IN TREK2A:TRPROU.F77
$USES USKIP IN TREK2A:TRSKIP.F77
$USES RANDOM IN TREK2A:TRRANF.PAS
$USES UPROMPT IN TREK2A:TRPROM.F77
$USES UCRAM IN TREK2A:TRCRAM.F77
$USES UCRAMI IN TREK2A:TRCRAI.F77
$USES UCRAMDM IN TREK2A:TRCRDM.F77
$USES UJA IN TREK2A:TRJA.F77
$USES UCRAMF IN TREK2B:TRCRAF.F77
$USES UTIMEWR IN TREK2B:TRTIMW.F77
$USES UFINISH IN TREK2B:TRFINI.F77
$USES UMOVE IN TREK2B:TRMOVE.F77
$USES UGETCD IN TREK2B:TRGETC.F77
C
      SUBROUTINE WARP(JWARPX)
$INCLUDE COMP1B2:TREKCO.F77
      CHARACTER*240 OLINE
      COMMON/MYBUF/OLINE
      CHARACTER*240 ITEM
      COMMON/SCNBF1/ITEM
      INTEGER BLOOEY,TWARP
      LOGICAL JA
      INTRINSIC SIN,COS,AMAX1,ABS
C
      IF (JWARPX.EQ.1) GO TO 20
C
      IDIDIT=0
      IF(DAMAGE(6) .GT. 10.0) GO TO 90
      IF(DAMAGE(6) .EQ. 0.0  .OR.  WARPFA .LE. 4.0) GO TO 3
      OLINE='ENGINEER SCOTT:  "SORRY, CAPTAIN.  UNTIL THIS DAMAGE'
      CALL PROUT(52)
      OLINE='  IS REPAIRED, I CAN ONLY GIVE YOU WARP 4."'
      CALL PROUT(43)
      RETURN
C--------READ IN COURSE AND DISTANCE
 3    CALL GETCD
      IF(DIREC .LT. 0) RETURN
C--------MAKE SURE STARSHIP HAS ENOUGH ENERGY TO MAKE THE TRIP
      POWER=(DIST+0.05)*WARPFA*WARPFA*WARPFA*(SHLDUP+1)
      IF(POWER .LT. ENERGY) GO TO 10
      CALL SKIP(1)
      OLINE='"ENGINEERING TO BRIDGE--'
      CALL PROUT(24)
      IF(SHLDUP.EQ.0 .OR. 0.5*POWER.GT.ENERGY) GO TO 5
      OLINE='   WE HAVEN''T THE ENERGY TO GO THAT FAR WITH THE SHIELDS
     + UP."'
      CALL PROUT(61)
      RETURN
 5    IWARP=(ENERGY/(DIST+0.05))**0.3333333333
      IF(IWARP .LE. 0) GO TO 8
      ITEM='  WE HAVEN''T THE ENERGY.  BUT WE COULD DO IT AT WARP '
      CALL CRAM(53)
      CALL CRAMI(IWARP,0)
      IF(SHLDUP.NE.0) GO TO 6
      ITEM='."'
      CALL CRAMDM(2)
      RETURN
 6    ITEM=','
      CALL CRAMDM(1)
      OLINE='  IF YOU''LL LOWER THE SHIELDS."'
      CALL PROUT(31)
      RETURN
 8    OLINE='  WE CAN''T DO IT, CAPTAIN.  WE HAVEN''T THE ENERGY."'
      CALL PROUT(51)
      RETURN
C--------MAKE SURE ENOUGH TIME IS LEFT FOR TRIP
 10   TIME=10.0*DIST/WFACSQ
      IF(TIME .LT. 0.80*REMTIM) GO TO 20
      CALL SKIP(1)
      OLINE='FIRST OFFICER SPOCK:  "CAPTAIN, I COMPUTE THAT SUCH'
      CALL PROUT(51)
      ITEM='  A TRIP WOULD REQUIRE APPROXIMATELY '
      CALL CRAM(37)
      CALL CRAMF(100.0*TIME/REMTIM,0,2)
      ITEM=' % OF OUR'
      CALL CRAMDM(9)
      OLINE='  REMAINING TIME.  ARE YOU SURE THIS IS WISE?"  '
      CALL PROMPT(48)
      IF(JA(1)) GO TO 20
      RETURN
C*
C     ENTRY WARPX
C*
 20   BLOOEY=0
      TWARP=0
      IF(WARPFA .LE. 6.0) GO TO 50
C--------DECIDE IF ENGINE DAMAGE WILL OCCUR
      PROB=DIST*(6.0-WARPFA)**2/66.666666666
      IF(PROB .GT. RANF()) BLOOEY=1
      IF(BLOOEY.NE.0) THEN
      RF=RANF()
      DIST=RF*DIST
      ENDIF
C----------DECIDE IF TIME WARP WILL OCCUR
      TWARP=0
      IF(WARPFA .LT. 10.0) GO TO 40
      IF(0.5*DIST .GT. RANF()) TWARP=1
 40   IF(BLOOEY .EQ. 0 .AND. TWARP .EQ. 0) GO TO 50
C--------IF ENGINE DAMAGE OR TIME WARP SHOULD OCCUR, CHECK PATH
      ANGLE=((15.0-DIREC)*0.5235998)
      DELTAX=-SIN(ANGLE)
      DELTAY=COS(ANGLE)
      BIGGER=AMAX1(ABS(DELTAX),ABS(DELTAY))
      DELTAX=DELTAX/BIGGER
      DELTAY=DELTAY/BIGGER
      N=10.0*DIST*BIGGER+0.5
      X=SECTX
      Y=SECTY
      IF(N .EQ. 0) GO TO 50
      DO 45 L=1,N
      X=X+DELTAX
      IX=X+0.5
      IF(IX .LT. 1 .OR. IX .GT. 10) GO TO 50
      Y=Y+DELTAY
      IY=Y+0.5
      IF(IY .LT. 1 .OR. IY .GT. 10) GO TO 50
      IF(QUAD(IX,IY) .EQ. IHDOT)  GO TO 45
      BLOOEY=0
      TWARP=0
 45   CONTINUE
C--------ACTIVATE WARP ENGINES AND PAY THE COST
 50   KSTUF(4)=0
      CALL MOVE
      IF(ALLDON.NE.0) RETURN
      ENERGY=ENERGY - DIST*WARPFA*WARPFA*WARPFA*(SHLDUP+1)
      IF(ENERGY .GT. 0) GO TO 55
      CALL FINISH(4)
      RETURN
 55   IF(KSTUF(4).EQ.0) TIME=10.0*DIST/WFACSQ
C--------ENTER TIME WARP
      IF(TWARP.NE.0) CALL TIMEWR
C--------DAMAGE WARP ENGINES
      IF(BLOOEY .EQ. 0) GO TO 60
      RF=RANF()
      DAMAGE(6)=DAMFAC*(3.0*RF+1.0)
      CALL SKIP(1)
      OLINE='"ENGINEERING TO BRIDGE--'
      CALL PROUT(24)
      OLINE='  SCOTT HERE.  THE WARP ENGINES ARE DAMAGED.'
      CALL PROUT(44)
      OLINE='  WE''LL HAVE TO REDUCE SPEED TO WARP 4." '
      CALL PROUT(41)
 60   IDIDIT=1
      RETURN
C--------NO WARP ENGINES
 90   CALL SKIP(1)
      OLINE='WARP ENGINES INOPERATIVE.'
      CALL PROUT(25)
C
      RETURN
      END
