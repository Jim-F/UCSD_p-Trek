$USES UPROUT IN TREK2A:TRPROU.F77
$USES USKIP IN TREK2A:TRSKIP.F77
$USES RANDOM IN TREK2A:TRRANF.PAS
$USES UCRAM IN TREK2A:TRCRAM.F77
$USES UCREND IN TREK2A:TRCREN.F77
$USES UCRAMDM IN TREK2A:TRCRDM.F77
$USES UCRAMLO IN TREK2A:TRCRML.F77
$USES UCRAMF IN TREK2B:TRCRAF.F77
$USES UCRAMSH IN TREK2B:TRCRSH.F77
$USES UFINISH IN TREK2B:TRFINI.F77
C
      SUBROUTINE SNOVA(INSX,INSY)
C
C       5-DEC-79
C       DON'T CHARGE PLAYER FOR BAD THINGS IF SUPERNOVA CAUSED BY
C       ENEMY ACTION
C
$INCLUDE COMP1B2:TREKCO.F77
      CHARACTER*240 OLINE
      COMMON/MYBUF/OLINE
      CHARACTER*240 ITEM
      COMMON/SCNBF1/ITEM
      INTEGER COMDEA
      INTRINSIC MOD
C
      NSX=INSX
      NSY=INSY
C--------IF SCHEDULED SUPERNOVA (INSX=INSY=0), SELECT STAR
      IF(INSX.NE.0) GO TO 50
      NUM=RANF()*INSTAR+1
      DO 10 NQX=1,8
      DO 10 NQY=1,8
      NUM=NUM-MOD(GALAXY(NQX,NQY),10)
      IF(NUM .LE. 0) GO TO 20
 10   CONTINUE
C--------IF STAR IS ALREADY GONE, RETURN EMPTY-HANDED
      RETURN
C--------IF STARSHIP IS IN THIS QUADRANT, CHOOSE STAR EXACTLY
 20   IF(NQX.NE.QUADX .OR. NQY.NE.QUADY) GO TO 70
C--------UNLESS STARSHIP JUST GOT HERE; THEN TREAT SUPERNOVA AS
C        OCCURRING WHILE EN ROUTE.
      IF(JUSTIN.NE.0) GO TO 70
      NUM=RANF()*MOD(GALAXY(NQX,NQY),10)+1
      DO 30 NSX=1,10
      DO 30 NSY=1,10
      IF(QUAD(NSX,NSY) .NE. IHSTAR) GO TO 30
      NUM=NUM-1
      IF(NUM .EQ. 0) GO TO 50
 30   CONTINUE
C--------PRINT RED ALERT (INCIPIENT SUPERNOVA) MESSAGE
 50   CALL SKIP(1)
      CALL REDALR
      ITEM='***INCIPIENT SUPERNOVA DETECTED AT'
      CALL CRAM(34)
      CALL CRAMLO(2,NSX,NSY)
      CALL CREND
      NQX=QUADX
      NQY=QUADY
C--------SUPERNOVA ADJACENT TO STARSHIP ENDS GAME
      DSQ=(NSX-SECTX)**2 + (NSY-SECTY)**2
      IF(DSQ .GT. 2.1) GO TO 80
      OLINE='EMERGENCY AUTOMATIC OVERRIDE ATTEMPTS T***************'
      CALL PROUT(54)
      CALL STARS
      ALLDON=1
      GO TO 80
C--------IF STARSHIP NOT IN SAME QUADRANT, JUST GET A MESSAGE
 70   IF(DAMAGE(9) .NE. 0) GO TO 80
      CALL SKIP(1)
      ITEM='MESSAGE FROM STARFLEET COMMAND          STARDATE '
      CALL CRAM(49)
      CALL CRAMF(DATE,0,1)
      CALL CREND
      ITEM='     SUPERNOVA IN'
      CALL CRAM(17)
      CALL CRAMLO(1,NQX,NQY)
      ITEM='; CAUTION ADVISED.'
      CALL CRAMDM(18)
C--------DESTROY ANY KLINGONS IN SUPERNOVAED QUADRANT
 80   NUM=GALAXY(NQX,NQY)
      KLDEAD=NUM/100
      COMDEA=0
      ISCDEA=0
      IF((NQX .NE. ISX) .OR. (NQY .NE. ISY)) GO TO 85
      NSCREM=0
      ISX=0
      ISY=0
      ISATB=0
      ISCATE=0
      ISCDEA=1
      FUTURE(6)=1E38
      FUTURE(7)=1E38
 85   IF(KLDEAD .EQ. 0) GO TO 100
      REMKL=REMKL-KLDEAD
      IF(REMCOM .EQ. 0) GO TO 100
      MAXLOO=REMCOM
      DO 90 L=1,MAXLOO
      IF(CX(L).NE.NQX .OR. CY(L).NE.NQY) GO TO 90
      CX(L)=CX(REMCOM)
      CY(L)=CY(REMCOM)
      CX(REMCOM)=0
      CX(REMCOM)=0
      REMCOM=REMCOM-1
      KLDEAD=KLDEAD-1
      COMDEA=1
      IF(REMCOM .EQ. 0) FUTURE(2)=1E38
 90   CONTINUE
C--------DESTROY ROMULANS AND PLANETS IN SUPERNOVAED QUADRANT.
 100  NUM=NEWSTU(NQX,NQY)
      NEWSTU(NQX,NQY)=0
      NRMDEA=NUM/10
      NROMRE=NROMRE-NRMDEA
      NPDEAD=NUM-NRMDEA*10
      IF(NPDEAD .EQ. 0) GO TO 109
      DO 106 L=1,INPLAN
      IF((PLNETS(L,1) .NE. NQX).OR. (PLNETS(L,2) .NE. NQY)) GO TO 106
      DO 105 I=1,5
 105  PLNETS(L,I)=0
 106  CONTINUE
C--------DESTROY ANY BASE IN SUPERNOVAED QUADRANT
 109  IF(REMBAS .EQ. 0) GO TO 120
      MAXLOO=REMBAS
      DO 110 L=1,MAXLOO
      IF(BASEQX(L).NE.NQX .OR. BASEQY(L).NE.NQY) GO TO 110
      BASEQX(L)=BASEQX(REMBAS)
      BASEQY(L)=BASEQY(REMBAS)
      BASEQX(REMBAS)=0
      BASEQY(REMBAS)=0
      REMBAS=REMBAS-1
 110  CONTINUE
C--------IF STARSHIP CAUSED SUPERNOVA, TALLY UP DESTRUCTION
 120  IF(INSX .EQ. 0) GO TO 130
      NUMBER=MOD(GALAXY(NQX,NQY),100)
      KILLK=KILLK+KLDEAD
      KILLC=KILLC+COMDEA
      NROMKL=NROMKL+NRMDEA
      NSCKIL=NSCKIL+ISCDEA
C--------IF ENEMY ACTION CAUSED SUPERNOVA, DON'T ASSESS ANY PENALTIES
      IF(IPHWHO.EQ.1)GO TO 130
      STARKL=STARKL+MOD(NUMBER,10)
      BASEKL=BASEKL+(NUMBER/10)
      NPLANK=NPLANK+NPDEAD
C--------MARK SUPERNOVA IN GALAXY AND IN STAR CHART
 130  IF(STARCH(NQX,NQY).GT.0 .AND. DAMAGE(9).NE.0)
     +   STARCH(NQX,NQY)=1000+GALAXY(NQX,NQY)
      IF(DAMAGE(9).EQ.0 .OR. (QUADX.EQ.NQX .AND. QUADY.EQ.NQY))
     +   STARCH(NQX,NQY)=1
      GALAXY(NQX,NQY)=1000
C--------IF SUPERNOVA DESTROYS LAST KLINGONS, GIVE SPECIAL MESSAGE
      IF(REMKL.NE.0 .OR. (NQX.EQ.QUADX .AND. NQY.EQ.QUADY)) GO TO 140
      CALL SKIP(2)
      OLINE='LUCKY YOU! '
      CALL PROUT(11)
      ITEM='A SUPERNOVA IN'
      CALL CRAM(14)
      CALL CRAMLO(1,NQX,NQY)
      ITEM=' HAS JUST DESTROYED THE LAST KLINGONS.'
      CALL CRAMDM(38)
      CALL FINISH(1)
      RETURN
C--------IF SOME KLINGONS REMAIN, CONTINUE (OR DIE IN SUPERNOVA)
 140  IF(ALLDON.NE.0) CALL FINISH(8)
C
      RETURN
      END
